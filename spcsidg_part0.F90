SUBROUTINE SPCSIDG_PART0 (YDGEOMETRY, YDDYN, YDRIP, KSPEC2V, ZSDIV, PSPDIVG,KMLOCSTA,KMLOCEND)
USE GEOMETRY_MOD , ONLY : GEOMETRY
USE PARKIND1     , ONLY : JPIM, JPRB
USE YOMHOOK      , ONLY : LHOOK, DR_HOOK, JPHOOK
USE YOMDYN       , ONLY : TDYN
USE YOMRIP       , ONLY : TRIP
USE YOMMP0       , ONLY : MYSETV

!     ------------------------------------------------------------------

IMPLICIT NONE

TYPE(GEOMETRY)    ,INTENT(IN)    :: YDGEOMETRY
TYPE(TDYN)        ,INTENT(IN)    :: YDDYN
TYPE(TRIP)        ,INTENT(IN)    :: YDRIP
INTEGER(KIND=JPIM),INTENT(IN)    :: KSPEC2V
REAL(KIND=JPRB),   INTENT(INOUT) :: ZSDIV (KSPEC2V,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB),   INTENT(INOUT) :: PSPDIVG(KSPEC2V,YDGEOMETRY%YRDIMV%NFLEVG)
INTEGER(KIND=JPIM),INTENT(IN)    :: KMLOCSTA
INTEGER(KIND=JPIM),INTENT(IN)    :: KMLOCEND

INTEGER(KIND=JPIM)               :: JMLOC

REAL(KIND=JPRB)  :: ZBDT

!     ------------------------------------------------------------------

INTEGER(KIND=JPIM):: IN,IM,ISTA,IEND,JSP,JLEV,IOFF

REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

!     -----------------------

IF (LHOOK) CALL DR_HOOK('SPCSIDG_PART0',0,ZHOOK_HANDLE)

ASSOCIATE(YDDIM=>YDGEOMETRY%YRDIM,YDDIMV=>YDGEOMETRY%YRDIMV,YDLAP=>YDGEOMETRY%YRLAP,YDMP=>YDGEOMETRY%YRMP)
ASSOCIATE(NSMAX=>YDDIM%NSMAX,NFLEVG=>YDDIMV%NFLEVG,SIHEG=>YDDYN%SIHEG,SIHEG2=>YDDYN%SIHEG2,&
& NSPSTAF=>YDMP%NSPSTAF,TDT=>YDRIP%TDT,RBTS2=>YDDYN%RBTS2,NPTRSVF=>YDMP%NPTRSVF)

#if defined(_OPENACC)
!$ACC DATA PRESENT(YDLAP%NVALUE,YDLAP%RLAPIN,YDLAP%RLAPDI,NFLEVG,NSMAX,YDDYN%SIVP)
!$ACC DATA PRESENT(PSPDIVG,ZSDIV,NSPSTAF,NPTRSVF,MYSETV,TDT,RBTS2)

  !$ACC PARALLEL PRIVATE(IM,ISTA,IEND) DEFAULT(NONE)
  ZBDT=RBTS2*TDT
  IOFF=NPTRSVF(MYSETV)-1
  !$ACC LOOP GANG COLLAPSE(2) 
  DO JMLOC=KMLOCSTA, KMLOCEND
    DO JLEV=1,NFLEVG
  
      IM=YDLAP%MYMS(JMLOC)
      ISTA=NSPSTAF(IM)
      IEND=ISTA+2*(NSMAX+1-IM)-1
      IF (IM > 0) THEN
        !$ACC LOOP VECTOR PRIVATE(IN) 
        DO JSP=ISTA,IEND
          IN=YDLAP%NVALUE(JSP+IOFF)
          ZSDIV(JSP,JLEV)=YDLAP%RLAPIN(IN)*PSPDIVG(JSP,JLEV)-ZBDT*ZSDIV(JSP,JLEV)
        ENDDO

      ELSE

        !$ACC LOOP VECTOR PRIVATE(IN)
        DO JSP=ISTA,IEND
          IN=YDLAP%NVALUE(JSP+IOFF)
          ZSDIV(JSP,JLEV)=PSPDIVG(JSP,JLEV)-ZBDT*YDLAP%RLAPDI(IN)*ZSDIV(JSP,JLEV)
        ENDDO

      ENDIF
    ENDDO
  ENDDO
  !$ACC END PARALLEL 
  !$ACC END DATA
  !$ACC END DATA
#else

ZBDT=RBTS2*TDT
IOFF=NPTRSVF(MYSETV)-1
DO JMLOC=KMLOCSTA,KMLOCEND
  IM=YDLAP%MYMS(JMLOC) 
  ISTA=NSPSTAF(IM)
  IEND=ISTA+2*(NSMAX+1-IM)-1

  IF (IM > 0) THEN
    !$OMP PARALLEL DO PRIVATE(JSP,JLEV,IN)
    DO JLEV=1,NFLEVG
      DO JSP=ISTA,IEND
        IN=YDLAP%NVALUE(JSP+IOFF)
        ZSDIV(JSP,JLEV)=YDLAP%RLAPIN(IN)*PSPDIVG(JSP,JLEV)-ZBDT*ZSDIV(JSP,JLEV)
      ENDDO
    ENDDO
    !$OMP END PARALLEL DO

  ELSE

    !$OMP PARALLEL DO PRIVATE(JSP,JLEV,IN)
    DO JLEV=1,NFLEVG
      DO JSP=ISTA,IEND
        IN=YDLAP%NVALUE(JSP+IOFF)
        ZSDIV(JSP,JLEV)=PSPDIVG(JSP,JLEV)-ZBDT*YDLAP%RLAPDI(IN)*ZSDIV(JSP,JLEV)
      ENDDO
    ENDDO
    !$OMP END PARALLEL DO

  ENDIF
ENDDO
#endif

END ASSOCIATE
END ASSOCIATE

IF (LHOOK) CALL DR_HOOK('SPCSIDG_PART0',1,ZHOOK_HANDLE)
END SUBROUTINE SPCSIDG_PART0

