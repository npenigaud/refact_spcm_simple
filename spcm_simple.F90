SUBROUTINE SPCM_SIMPLE (YDGEOMETRY,YDMODEL,PSPSP,PSPVOR,PSPDIV,PSPT,PSPSPD,PSPSVD)

USE TYPE_MODEL         , ONLY : MODEL
USE GEOMETRY_MOD       , ONLY : GEOMETRY
USE PARKIND1           , ONLY : JPIM, JPRB
USE YOMHOOK            , ONLY : LHOOK, DR_HOOK, JPHOOK
use YOMMP0             , ONLY : MYSETN

!     ------------------------------------------------------------------

IMPLICIT NONE

TYPE(GEOMETRY)      ,INTENT(IN)    :: YDGEOMETRY
TYPE(MODEL)         ,INTENT(INOUT) :: YDMODEL
REAL(KIND=JPRB)     ,INTENT(INOUT) :: PSPSP (YDGEOMETRY%YRDIM%NSPEC2)
REAL(KIND=JPRB)     ,INTENT(INOUT) :: PSPVOR(YDGEOMETRY%YRDIMV%NFLEVL,YDGEOMETRY%YRDIM%NSPEC2)
REAL(KIND=JPRB)     ,INTENT(INOUT) :: PSPDIV(YDGEOMETRY%YRDIMV%NFLEVL,YDGEOMETRY%YRDIM%NSPEC2)
REAL(KIND=JPRB)     ,INTENT(INOUT) :: PSPT  (YDGEOMETRY%YRDIMV%NFLEVL,YDGEOMETRY%YRDIM%NSPEC2)
REAL(KIND=JPRB)     ,INTENT(INOUT) :: PSPSPD(YDGEOMETRY%YRDIMV%NFLEVL,YDGEOMETRY%YRDIM%NSPEC2)
REAL(KIND=JPRB)     ,INTENT(INOUT) :: PSPSVD(YDGEOMETRY%YRDIMV%NFLEVL,YDGEOMETRY%YRDIM%NSPEC2)

#include "spcsi_cor.intfb.h"
#include "spcsi_str.intfb.h"
#include "trmtos.intfb.h"
#include "trstom.intfb.h"
#include "spcimpfinit.intfb.h"
#include "spcimpfpost.intfb.h"

#if defined(_OPENACC)
REAL(KIND=JPRB),ALLOCATABLE  :: PARAM_MXTURE(:,:,:)
#else
REAL(KIND=JPRB)  :: SIMIT(YDGEOMETRY%YRDIMV%NFLEVG,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)  :: SIMOT(YDGEOMETRY%YRDIMV%NFLEVG,YDGEOMETRY%YRDIMV%NFLEVG)
#endif

REAL(KIND=JPRB), ALLOCATABLE :: ZSPVORG2(:,:)
REAL(KIND=JPRB), ALLOCATABLE :: ZSPDIVG2(:,:)
REAL(KIND=JPRB), ALLOCATABLE :: ZSPTG2  (:,:)
REAL(KIND=JPRB), ALLOCATABLE :: ZSPSPDG2(:,:)
REAL(KIND=JPRB), ALLOCATABLE :: ZSPSVDG2(:,:)
REAL(KIND=JPRB), ALLOCATABLE :: ZSPSPG2 (:)
REAL(KIND=JPRB), ALLOCATABLE :: ZSPVORG(:,:)
REAL(KIND=JPRB), ALLOCATABLE :: ZSPDIVG(:,:)
REAL(KIND=JPRB), ALLOCATABLE :: ZSPTG  (:,:)
REAL(KIND=JPRB), ALLOCATABLE :: ZSPSPDG(:,:)
REAL(KIND=JPRB), ALLOCATABLE :: ZSPSVDG(:,:)
REAL(KIND=JPRB), ALLOCATABLE :: ZSPSPG (:)
REAL(KIND=JPRB), ALLOCATABLE :: ZSPTNDSI_VORG(:,:)
REAL(KIND=JPRB), ALLOCATABLE :: ZSPTNDSI_DIVG(:,:)
REAL(KIND=JPRB), ALLOCATABLE :: ZSPTNDSI_TG(:,:)
REAL(KIND=JPRB), ALLOCATABLE :: ZSPAUX (:,:)
REAL(KIND=JPRB), ALLOCATABLE :: ZSPAUXG(:,:)
REAL(KIND=JPRB), ALLOCATABLE :: PSPVOR2(:,:)
REAL(KIND=JPRB), ALLOCATABLE :: PSPDIV2(:,:)
REAL(KIND=JPRB), ALLOCATABLE :: PSPT2  (:,:)
REAL(KIND=JPRB), ALLOCATABLE :: PSPSPD2(:,:)
REAL(KIND=JPRB), ALLOCATABLE :: PSPSVD2(:,:)
REAL(KIND=JPRB), ALLOCATABLE :: PSPSP2 (:)

LOGICAL :: LLONEM, LLTRANSPOSE

INTEGER(KIND=JPIM) :: IM, ISPEC2V
INTEGER (KIND=JPIM) :: JMLOC, ISTA, IEND

REAL(KIND=JPHOOK) ::  ZHOOK_HANDLE,ZHOOK_HANDLE2,ZHOOK_HANDLE3
INTEGER(KIND=JPIM)::  JCNT1,JCNT2,IS0,IS02,ITAILLE

IF (LHOOK) CALL DR_HOOK('SPCM_SIMPLE',0,ZHOOK_HANDLE)

ASSOCIATE(YDDIM=>YDGEOMETRY%YRDIM,YDDIMV=>YDGEOMETRY%YRDIMV,  YDMP=>YDGEOMETRY%YRMP, YDDYN=>YDMODEL%YRML_DYN%YRDYN,   &
&  YDDYNA=>YDMODEL%YRML_DYN%YRDYNA, YDLDDH=>YDMODEL%YRML_DIAG%YRLDDH, YDSPDDH=>YDMODEL%YRML_DIAG%YRSPDDH, &
&  YDLAP=>YDGEOMETRY%YRLAP)

ASSOCIATE(NFLEVG=>YDDIMV%NFLEVG, NSPEC2V=>YDMP%NSPEC2V, NSPEC2VF=>YDMP%NSPEC2VF, LIMPF=>YDDYN%LIMPF, &
& NFLSUR=>YDDIMV%NFLSUR, NSPEC2=>YDDIM%NSPEC2, NUMP=>YDDIM%NUMP, NSMAX=>YDDIM%NSMAX,NFLEVL=>YDDIMV%NFLEVL, &
& SIMI=>YDDYN%SIMI,SIMO=>YDDYN%SIMO,SIHEG=>YDDYN%SIHEG,SIHEG2=>YDDYN%SIHEG2, &
& NPTRMF=>YDMP%NPTRMF,LSIDG=>YDDYN%LSIDG)

#if defined(_OPENACC)
LLTRANSPOSE=.FALSE.
#else
LLTRANSPOSE=.TRUE.
#endif

#if defined(_OPENACC)
IF (LSIDG) THEN
  ITAILLE=0
  DO JMLOC=NPTRMF(MYSETN),NPTRMF(MYSETN+1)-1
    IM=YDLAP%MYMS(JMLOC)  
    ITAILLE=MAX(ITAILLE,NFLEVG*(NSMAX+1-IM))
  ENDDO

  ALLOCATE(PARAM_MXTURE(ITAILLE,NPTRMF(MYSETN+1)-1,5))
  !$ACC ENTER DATA CREATE(PARAM_MXTURE)
  PARAM_MXTURE(:,:,:)=0.0_JPRB

  !$ACC PARALLEL PRIVATE(JCNT2)
  DO JMLOC=NPTRMF(MYSETN),NPTRMF(MYSETN+1)-1
    IM=YDLAP%MYMS(JMLOC)  
    IS0=YDLAP%NSE0L(JMLOC)
    IS02=0
    IF (.TRUE.) THEN
      !$ACC LOOP PRIVATE(JCNT2)
      DO JCNT1=IS0+1,IS0+NSMAX+1-im
        DO JCNT2=1,NFLEVG
          PARAM_MXTURE(JCNT1-IS0-1+1+(NSMAX+1-IM)*(JCNT2-1),JMLOC,1)=SIHEG(JCNT2,JCNT1,1) 
          PARAM_MXTURE(JCNT1-IS0-1+1+(NSMAX+1-IM)*(JCNT2-1),JMLOC,2)=SIHEG(JCNT2,JCNT1,2) 
          PARAM_MXTURE(JCNT1-IS0-1+1+(NSMAX+1-IM)*(JCNT2-1),JMLOC,3)=SIHEG(JCNT2,JCNT1,3) 
        ENDDO
      ENDDO
      

      !$ACC LOOP PRIVATE(JCNT2)
      DO JCNT1=IS02+1,IS02+NSMAX+1-IM
        DO JCNT2=1,NFLEVG
          PARAM_MXTURE(JCNT1-IS02-1+1+(NSMAX+1-IM)*(JCNT2-1),JMLOC,4)=SIHEG2(JCNT2,JCNT1,2) 
          PARAM_MXTURE(JCNT1-IS02-1+1+(NSMAX+1-IM)*(JCNT2-1),JMLOC,5)=SIHEG2(JCNT2,JCNT1,3) 
        ENDDO
      ENDDO
   
    ELSE
      !$ACC LOOP PRIVATE(JCNT2)
      DO JCNT1=IS0+1,IS0+NSMAX+1-im
        DO JCNT2=1,NFLEVG
          PARAM_MXTURE((JCNT1-IS0-1)*NFLEVG+1+(JCNT2-1),JMLOC,1)=SIHEG(JCNT2,JCNT1,1) 
          PARAM_MXTURE((JCNT1-IS0-1)*NFLEVG+1+(JCNT2-1),JMLOC,2)=SIHEG(JCNT2,JCNT1,2) 
          PARAM_MXTURE((JCNT1-IS0-1)*NFLEVG+1+(JCNT2-1),JMLOC,3)=SIHEG(JCNT2,JCNT1,3) 
        ENDDO
      ENDDO
    

      !$ACC LOOP PRIVATE(JCNT2)
      DO JCNT1=IS02+1,IS02+NSMAX+1-IM
        DO JCNT2=1,NFLEVG
          PARAM_MXTURE((JCNT1-IS02-1)*NFLEVG+1+(JCNT2-1),JMLOC,4)=SIHEG2(JCNT2,JCNT1,2) 
          PARAM_MXTURE((JCNT1-IS02-1)*NFLEVG+1+(JCNT2-1),JMLOC,5)=SIHEG2(JCNT2,JCNT1,3) 
        ENDDO
      ENDDO
      

    ENDIF !!choix du sens de transposition

  ENDDO   !!JMLOC
  !$ACC END PARALLEL

ELSE      !! non lsidg lsidg 
  ALLOCATE(PARAM_MXTURE(1,1,1))
  !$ACC ENTER DATA CREATE(PARAM_MXTURE)
ENDIF


#else
!$OMP PARALLEL DO PRIVATE(JCNT1,JCNT2)
DO JCNT1=1,NFLEVG
  DO JCNT2=1,NFLEVG
    SIMIT(JCNT1,JCNT2)=SIMI(JCNT2,JCNT1)
  ENDDO
ENDDO
!$OMP END PARALLEL DO

!$OMP PARALLEL DO PRIVATE(JCNT1,JCNT2)
DO JCNT1=1,NFLEVG
  DO JCNT2=1,NFLEVG
    SIMOT(JCNT1,JCNT2)=SIMO(JCNT2,JCNT1)
  ENDDO
ENDDO
!$OMP END PARALLEL DO
#endif

LLONEM = .NOT. LIMPF

IF (LLONEM) THEN
  ISPEC2V=NSPEC2VF
ELSE
  ISPEC2V=NSPEC2V
ENDIF

ALLOCATE(ZSPVORG2(NFLEVG,ISPEC2V))
ALLOCATE(ZSPDIVG2(NFLEVG,ISPEC2V))
ALLOCATE(ZSPTG2  (NFLEVG,ISPEC2V))
ALLOCATE(ZSPSPDG2(NFLEVG,ISPEC2V))
ALLOCATE(ZSPSVDG2(NFLEVG,ISPEC2V))
ALLOCATE(ZSPSPG2 (ISPEC2V))

ALLOCATE(PSPVOR2(NSPEC2,NFLEVL))
ALLOCATE(PSPDIV2(NSPEC2,NFLEVL))
ALLOCATE(PSPT2  (NSPEC2,NFLEVL))
ALLOCATE(PSPSPD2(NSPEC2,NFLEVL))
ALLOCATE(PSPSVD2(NSPEC2,NFLEVL))
ALLOCATE(PSPSP2 (NSPEC2))

ALLOCATE(ZSPVORG(ISPEC2V,NFLEVG))
ALLOCATE(ZSPDIVG(ISPEC2V,NFLEVG))
ALLOCATE(ZSPTG  (ISPEC2V,NFLEVG))
ALLOCATE(ZSPSPDG(ISPEC2V,NFLEVG))
ALLOCATE(ZSPSVDG(ISPEC2V,NFLEVG))
ALLOCATE(ZSPSPG (ISPEC2V))

ALLOCATE(ZSPTNDSI_VORG(1,1))
ALLOCATE(ZSPTNDSI_DIVG(1,1))
ALLOCATE(ZSPTNDSI_TG(1,1))

IF (LIMPF) THEN
  ALLOCATE(ZSPAUX(NFLSUR,NSPEC2))
  ALLOCATE(ZSPAUXG(NFLEVG,ISPEC2V))

!$OMP PARALLEL DO SCHEDULE(STATIC,1) PRIVATE(JMLOC,IM,ISTA,IEND)
  DO JMLOC=1,NUMP
    IM=YDLAP%MYMS(JMLOC)
    ISTA=YDLAP%NASM0(IM)
    IEND=ISTA+2*(NSMAX+1-IM)-1
    CALL SPCIMPFINIT(YDMODEL%YRCST,YDGEOMETRY,YDMODEL%YRML_GCONF%YRRIP,YDDYN,IM,ISTA,IEND,PSPVOR,ZSPAUX)
  ENDDO
!$OMP END PARALLEL DO

  !! LLTRANSPOSE to .false. as no data transposition implemented yet
  CALL TRMTOS(YDGEOMETRY,YDDYNA%LNHDYN,YDDYNA%LNHX,.FALSE.,&
    & PSPVOR=PSPVOR,PSPDIV=PSPDIV,PSPT=PSPT,PSPSPD=PSPSPD,&
    & PSPSVD=PSPSVD,PSPSP=PSPSP,PSPAUX=ZSPAUX,& 
    & PSPVORG=ZSPVORG,PSPDIVG=ZSPDIVG,PSPTG=ZSPTG,PSPSPDG=ZSPSPDG,&
    & PSPSVDG=ZSPSVDG,PSPSPG=ZSPSPG,PSPAUXG=ZSPAUXG,&
    & LDFULLM=LLONEM)

  CALL SPCSI_COR(YDGEOMETRY, YDMODEL%YRCST, YDLDDH, YDMODEL%YRML_GCONF%YRRIP, YDDYN, ISPEC2V, &
  & LLONEM, ZSPVORG, ZSPDIVG, ZSPTG, ZSPSPG, ZSPTNDSI_VORG, ZSPTNDSI_DIVG, ZSPTNDSI_TG,       &
  & PSPAUXG=ZSPAUXG)

  CALL TRSTOM(&
    & YDGEOMETRY,YDDYNA%LNHDYN,YDDYNA%LNHX,.FALSE.,&
    & PSPVORG=ZSPVORG,PSPDIVG=ZSPDIVG,PSPTG=ZSPTG,PSPSPDG=ZSPSPDG,&
    & PSPSVDG=ZSPSVDG,PSPSPG=ZSPSPG,&
    & PSPVOR=PSPVOR,PSPDIV=PSPDIV,PSPT=PSPT,PSPSPD=PSPSPD,&
    & PSPSVD=PSPSVD,PSPSP=PSPSP,&
    & LDFULLM=LLONEM,LDNEEDPS=.TRUE.)  

!$OMP PARALLEL DO SCHEDULE(STATIC,1) PRIVATE(JMLOC,IM,ISTA,IEND)
    DO JMLOC=1,NUMP
      IM=YDLAP%MYMS(JMLOC)
      ISTA=YDLAP%NASM0(IM)
      IEND=ISTA+2*(NSMAX+1-IM)-1
      CALL SPCIMPFPOST(YDMODEL%YRCST,YDGEOMETRY,YDMODEL%YRML_GCONF%YRRIP,YDDYN,IM,ISTA,IEND,PSPDIV,PSPVOR)
    ENDDO
!$OMP END PARALLEL DO

ELSE

#if defined(_OPENACC)
    PSPSP2(:)=PSPSP(:)
    DO JCNT1=1,NSPEC2
      DO JCNT2=1,NFLEVL
        PSPVOR2(JCNT1,JCNT2)=PSPVOR(JCNT2,JCNT1)
        PSPDIV2(JCNT1,JCNT2)=PSPDIV(JCNT2,JCNT1)
        PSPT2(JCNT1,JCNT2)=PSPT(JCNT2,JCNT1)
        PSPSPD2(JCNT1,JCNT2)=PSPSPD(JCNT2,JCNT1)
        PSPSVD2(JCNT1,JCNT2)=PSPSVD(JCNT2,JCNT1)
      ENDDO
    ENDDO
#endif

    IF (LHOOK) CALL DR_HOOK('SPCM_SIMPLE_transferts1a',0,ZHOOK_HANDLE2)
    !$ACC DATA CREATE(ZSPVORG,ZSPDIVG,ZSPTG,ZSPSPDG,ZSPSVDG,ZSPSPG) 
    IF (LHOOK) CALL DR_HOOK('SPCM_SIMPLE_transferts1a',1,ZHOOK_HANDLE2)
    IF (LHOOK) CALL DR_HOOK('SPCM_SIMPLE_transferts1b',0,ZHOOK_HANDLE2)
    !$ACC DATA COPY(PSPVOR2,PSPDIV2,PSPT2,PSPSPD2,PSPSVD2,PSPSP2)
    IF (LHOOK) CALL DR_HOOK('SPCM_SIMPLE_transferts1b',1,ZHOOK_HANDLE2)

  IF (LHOOK) CALL DR_HOOK('SPCM_SIMPLE_utile',0,ZHOOK_HANDLE3)

#if defined(_OPENACC)

  CALL TRMTOS(YDGEOMETRY,YDDYNA%LNHDYN,YDDYNA%LNHX,LLTRANSPOSE,&
    & PSPVOR=PSPVOR2,PSPDIV=PSPDIV2,PSPT=PSPT2,PSPSPD=PSPSPD2,&
    & PSPSVD=PSPSVD2,PSPSP=PSPSP2,&
    & PSPVORG=ZSPVORG,PSPDIVG=ZSPDIVG,PSPTG=ZSPTG,PSPSPDG=ZSPSPDG,&
    & PSPSVDG=ZSPSVDG,PSPSPG=ZSPSPG,&
    & LDFULLM=LLONEM)

  CALL SPCSI_STR(YDGEOMETRY, YDMODEL%YRCST, YDLDDH, YDMODEL%YRML_GCONF%YRRIP, YDDYN, ISPEC2V, &
  & ZSPVORG, ZSPDIVG, ZSPTG, ZSPSPG, ZSPTNDSI_VORG, ZSPTNDSI_DIVG, ZSPTNDSI_TG,&
  & PARAM_MXTURE)

  CALL TRSTOM(&
    & YDGEOMETRY,YDDYNA%LNHDYN,YDDYNA%LNHX,LLTRANSPOSE,&
    & PSPVORG=ZSPVORG,PSPDIVG=ZSPDIVG,PSPTG=ZSPTG,PSPSPDG=ZSPSPDG,&
    & PSPSVDG=ZSPSVDG,PSPSPG=ZSPSPG,&
    & PSPVOR=PSPVOR2,PSPDIV=PSPDIV2,PSPT=PSPT2,PSPSPD=PSPSPD2,&
    & PSPSVD=PSPSVD2,PSPSP=PSPSP2,&
    & LDFULLM=LLONEM,LDNEEDPS=.TRUE.)  

#else 

IF (LLTRANSPOSE) THEN

  CALL TRMTOS(YDGEOMETRY,YDDYNA%LNHDYN,YDDYNA%LNHX,LLTRANSPOSE,&
    & PSPVOR=PSPVOR,PSPDIV=PSPDIV,PSPT=PSPT,PSPSPD=PSPSPD,&
    & PSPSVD=PSPSVD,PSPSP=PSPSP,&
    & PSPVORG=ZSPVORG,PSPDIVG=ZSPDIVG,PSPTG=ZSPTG,PSPSPDG=ZSPSPDG,&
    & PSPSVDG=ZSPSVDG,PSPSPG=ZSPSPG,&
    & LDFULLM=LLONEM)

  CALL SPCSI_STR(YDGEOMETRY, YDMODEL%YRCST, YDLDDH, YDMODEL%YRML_GCONF%YRRIP, YDDYN, ISPEC2V, &
  & ZSPVORG, ZSPDIVG, ZSPTG, ZSPSPG, ZSPTNDSI_VORG, ZSPTNDSI_DIVG, ZSPTNDSI_TG,&
  & SIMIT,SIMOT)

  CALL TRSTOM(&
    & YDGEOMETRY,YDDYNA%LNHDYN,YDDYNA%LNHX,LLTRANSPOSE,&
    & PSPVORG=ZSPVORG,PSPDIVG=ZSPDIVG,PSPTG=ZSPTG,PSPSPDG=ZSPSPDG,&
    & PSPSVDG=ZSPSVDG,PSPSPG=ZSPSPG,&
    & PSPVOR=PSPVOR,PSPDIV=PSPDIV,PSPT=PSPT,PSPSPD=PSPSPD,&
    & PSPSVD=PSPSVD,PSPSP=PSPSP,&
    & LDFULLM=LLONEM,LDNEEDPS=.TRUE.)  

ELSE

    PSPSP2(:)=PSPSP(:)
    DO JCNT1=1,NSPEC2
      DO JCNT2=1,NFLEVL
        PSPVOR2(JCNT1,JCNT2)=PSPVOR(JCNT2,JCNT1)
        PSPDIV2(JCNT1,JCNT2)=PSPDIV(JCNT2,JCNT1)
        PSPT2(JCNT1,JCNT2)=PSPT(JCNT2,JCNT1)
        PSPSPD2(JCNT1,JCNT2)=PSPSPD(JCNT2,JCNT1)
        PSPSVD2(JCNT1,JCNT2)=PSPSVD(JCNT2,JCNT1)
      ENDDO
    ENDDO

  CALL TRMTOS(YDGEOMETRY,YDDYNA%LNHDYN,YDDYNA%LNHX,LLTRANSPOSE,&
    & PSPVOR=PSPVOR2,PSPDIV=PSPDIV2,PSPT=PSPT2,PSPSPD=PSPSPD2,&
    & PSPSVD=PSPSVD2,PSPSP=PSPSP2,&
    & PSPVORG=ZSPVORG,PSPDIVG=ZSPDIVG,PSPTG=ZSPTG,PSPSPDG=ZSPSPDG,&
    & PSPSVDG=ZSPSVDG,PSPSPG=ZSPSPG,&
    & LDFULLM=LLONEM)

  CALL SPCSI_STR(YDGEOMETRY, YDMODEL%YRCST, YDLDDH, YDMODEL%YRML_GCONF%YRRIP, YDDYN, ISPEC2V, &
  & ZSPVORG, ZSPDIVG, ZSPTG, ZSPSPG, ZSPTNDSI_VORG, ZSPTNDSI_DIVG, ZSPTNDSI_TG,&
  & SIMIT,SIMOT)

  CALL TRSTOM(&
    & YDGEOMETRY,YDDYNA%LNHDYN,YDDYNA%LNHX,LLTRANSPOSE,&
    & PSPVORG=ZSPVORG,PSPDIVG=ZSPDIVG,PSPTG=ZSPTG,PSPSPDG=ZSPSPDG,&
    & PSPSVDG=ZSPSVDG,PSPSPG=ZSPSPG,&
    & PSPVOR=PSPVOR2,PSPDIV=PSPDIV2,PSPT=PSPT2,PSPSPD=PSPSPD2,&
    & PSPSVD=PSPSVD2,PSPSP=PSPSP2,&
    & LDFULLM=LLONEM,LDNEEDPS=.TRUE.) 

    PSPSP(:)=PSPSP2(:)
    DO JCNT2=1,NSPEC2
      DO JCNT1=1,NFLEVL
        PSPVOR(JCNT1,JCNT2)=PSPVOR2(JCNT2,JCNT1)
        PSPDIV(JCNT1,JCNT2)=PSPDIV2(JCNT2,JCNT1)
        PSPT(JCNT1,JCNT2)=PSPT2(JCNT2,JCNT1)
        PSPSPD(JCNT1,JCNT2)=PSPSPD2(JCNT2,JCNT1)
        PSPSVD(JCNT1,JCNT2)=PSPSVD2(JCNT2,JCNT1)
      ENDDO
    ENDDO


ENDIF

#endif

    IF (LHOOK) CALL DR_HOOK('SPCM_SIMPLE_utile',1,ZHOOK_HANDLE3)

    IF (LHOOK) CALL DR_HOOK('SPCM_SIMPLE_transferts2b',0,ZHOOK_HANDLE2)
    !$ACC END DATA
    IF (LHOOK) CALL DR_HOOK('SPCM_SIMPLE_transferts2b',1,ZHOOK_HANDLE2)
    IF (LHOOK) CALL DR_HOOK('SPCM_SIMPLE_transferts2a',0,ZHOOK_HANDLE2)
    !$ACC END DATA
    IF (LHOOK) CALL DR_HOOK('SPCM_SIMPLE_transferts2a',1,ZHOOK_HANDLE2)

#if defined(_OPENACC)
    PSPSP(:)=PSPSP2(:)
    DO JCNT2=1,NSPEC2
      DO JCNT1=1,NFLEVL
        PSPVOR(JCNT1,JCNT2)=PSPVOR2(JCNT2,JCNT1)
        PSPDIV(JCNT1,JCNT2)=PSPDIV2(JCNT2,JCNT1)
        PSPT(JCNT1,JCNT2)=PSPT2(JCNT2,JCNT1)
        PSPSPD(JCNT1,JCNT2)=PSPSPD2(JCNT2,JCNT1)
        PSPSVD(JCNT1,JCNT2)=PSPSVD2(JCNT2,JCNT1)
      ENDDO
    ENDDO

#endif

ENDIF

IF (ALLOCATED(ZSPVORG)) DEALLOCATE(ZSPVORG)
IF (ALLOCATED(ZSPDIVG)) DEALLOCATE(ZSPDIVG)
IF (ALLOCATED(ZSPTG))   DEALLOCATE(ZSPTG)
IF (ALLOCATED(ZSPSPDG)) DEALLOCATE(ZSPSPDG)
IF (ALLOCATED(ZSPSVDG)) DEALLOCATE(ZSPSVDG)
IF (ALLOCATED(ZSPSPG))  DEALLOCATE(ZSPSPG)

IF (ALLOCATED(ZSPVORG2)) DEALLOCATE(ZSPVORG2)
IF (ALLOCATED(ZSPDIVG2)) DEALLOCATE(ZSPDIVG2)
IF (ALLOCATED(ZSPTG2))   DEALLOCATE(ZSPTG2)
IF (ALLOCATED(ZSPSPDG2)) DEALLOCATE(ZSPSPDG2)
IF (ALLOCATED(ZSPSVDG2)) DEALLOCATE(ZSPSVDG2)
IF (ALLOCATED(ZSPSPG2))  DEALLOCATE(ZSPSPG2)

if (allocated(PSPVOR2)) DEALLOCATE(PSPVOR2)
if (allocated(PSPDIV2)) DEALLOCATE(PSPDIV2)
if (allocated(PSPT2)) DEALLOCATE(PSPT2)
if (allocated(PSPSPD2)) DEALLOCATE(PSPSPD2)
if (allocated(PSPSVD2)) DEALLOCATE(PSPSVD2)
if (allocated(PSPSP2)) DEALLOCATE(PSPSP2)

IF (ALLOCATED(ZSPTNDSI_VORG)) DEALLOCATE(ZSPTNDSI_VORG)
IF (ALLOCATED(ZSPTNDSI_DIVG)) DEALLOCATE(ZSPTNDSI_DIVG)
IF (ALLOCATED(ZSPTNDSI_TG))   DEALLOCATE(ZSPTNDSI_TG)
#if defined(_OPENACC)
  !$ACC EXIT DATA DELETE(PARAM_MXTURE)
  IF (ALLOCATED(PARAM_MXTURE)) DEALLOCATE(PARAM_MXTURE)
#endif

END ASSOCIATE
END ASSOCIATE

IF (LHOOK) CALL DR_HOOK('SPCM_SIMPLE',1,ZHOOK_HANDLE)

END SUBROUTINE SPCM_SIMPLE

